#!/usr/bin/env zsh
#
# adiff -- and start a vimdiff between the two versions of
#          adventure.py or test_game.py
#
#
# usage:
#
# adiff -h|--help [--last|--fin|--version VERSION] [FILE]

parse() {
  local sn=1
  # echo "$@"

  [[ $# -eq 0 ]] && mode=usage

  while [[ $# -gt 0 ]]; do
    case "$1" in
      -l|--last)       mode=last                           ;;
      -f|--fin)        mode=fin                            ;;
      -v|--version)    mode=version version="$2" sn=2      ;;
      -h|--help)       mode=help                           ;;
      *)
        [[ -z "${file}" ]] && file="$1"
                                                           ;;
    esac
    # echo $# "${@}"
    shift $sn
    sn=1
  done

  [[ "$file" == "test" ]] && file="test_game"
  : "${file:=adventure}"
}

get_last() {
  for f in "${srcdir}/${file}-"*(n); do
    [[ "${f##*/}" == "${current}" ]] && break
    diff_file="${f##*/}"
  done
}

get_final() {
  local this_version major
  this_version="${current##*-}"        # x.x.py
  this_version="${this_version%.py}"   # x.x
  major="${this_version%.*}"           # x

  diff_file="${file}-${major}.py"
}

get_version() {
  diff_file="${file}-${version}.py"
}

usage() {
  cat <<EOF

   adiff -- and start a vimdiff between the two versions of
            adventure.py or test_game.py

   usage:

     adiff [--last|--fin|--version VERSION] [FILE]
     adiff -h|--help
EOF
}

help() {
  usage
  cat <<EOF

     -l,--last      diff the last version of this FILE
     -f,--fin       diff the final version of this FILE
     -v,--version   diff the specific version VERSION of FILE
     -h,--help      print this help
     FILE           which file to generate the diff for
                    adventure|test[_game]
                    (default: adventure)
EOF
}

main() {
  local file diff_file current srcdir version

  parse ${1:+"$@"}

  srcdir="pythonclass/adventure"

  current="$(readlink -f "${file}.py")"
  current="${current##*/}"

  case "${mode}" in
    usage)    usage; exit 1 ;;
    last)     get_last      ;;
    fin)      get_final     ;;
    version)  get_version   ;;
    help)     help ; exit   ;;
  esac

  for f in "${diff_file}" "${current}"; do
    if [[ ! -f "${srcdir}/${f}" ]]; then
      echo "file not found: ${f}"
      exit 1
    fi
  done

  vimdiff "${srcdir}/${diff_file}" "${srcdir}/${current}"
}

main ${1:+"$@"}
