#!/usr/bin/env zsh
#
# avim -- edit a version or versions of adventure.py and/or test_game.py
#
#
# usage:
#
# avim -h|--help [-d|--dry-run] [--last|--fin|--version VERSION] [FILE...]

setopt SH_WORD_SPLIT

parse() {
  local sn=1

  while [[ $# -gt 0 ]]; do
    arg="$1"
    case "${arg}" in
      -d|--dryrun)     is_dryrun_mode=1                    ;;
      -l|--last)       mode=last                           ;;
      -f|--fin)        mode=fin                            ;;
      -v|--version)    mode=version version="$2" sn=2      ;;
      -h|--help)       mode=help                           ;;
      *)
        [[ "${arg}" == "test" ]] && arg="test_game"
        [[ "${arg}" == "game" ]] && arg="adventure"

        files+=( "${arg}" )
    esac
    shift $sn
    sn=1
  done

  : "${mode:=symlinkd}"

  [[ ${#files} -eq 0 ]] && files=( adventure )
}

get_symlinkd() {
  local file version _path

  for file in ${files[@]}; do
      if [[ "${file}" == "doc" ]]; then
        get_final
        _path="${docdir}/part-${versions}.md"
      else
        _path="${file}.py"
      fi


    if [[ ! -f "${_path}" ]]; then
      err "File not found: ${_path}"
      exit 1
    fi

    paths+=( "${_path}" )
  done

  if [[ ${#paths[@]} -eq 0 ]]; then
    err "No files found." >&2 && echo
    usage
    exit 1
  fi
}

get_last() {
  local f last_file

  for f in "${srcdir}/${file}-"*(n); do
    if [[ "${f##*/}" == "${current}" ]]; then
      last_file="${last_file##*-}"               # x.x.py
      versions+=( "${last_file%.py}" )           # x.x
      break
    fi

    last_file="${f}"
  done
}

get_final() {
  local this_version major

  this_version="${current##*-}"        # x.x.py
  this_version="${this_version%.py}"   # x.x
  major="${this_version%.*}"           # x

  versions+=( "${major}" )
}

get_versions() {
  local v

  if [[ "${version}" =~ , ]]; then
    for v in ${version//,/ } ; do  # split on ,
      versions+=( "${v}" )
    done
  else
    versions=( "$version" )
  fi
}

usage() {
  <<EOF
     Usage: avim [--last|--fin|--version VERSION] [FILE]
            avim -h|--help
EOF
}

help() {
  <<EOF

   avim -- and open adventure.py and/or test_game.py using nvim

EOF

  usage

  <<EOF

     OPTIONS

     -l,--last      diff the last version of this FILE
     -f,--fin       diff the final version of this FILE
     -v,--version   diff the specific version VERSION of FILE
     -h,--help      print this help
     FILE           which file to generate the diff for
                    adventure|test[_game]
                    (defaults to adventure, in current dir without -l|-f|-v)

EOF
}

get_paths() {
  local file version _path

  for file in ${files[@]}; do
    for version in ${versions}; do
      if [[ "${file}" == "doc" ]]; then
        _path="${docdir}/part-${version}.md"
      else
        _path="${srcdir}/${file}-${version}.py"
      fi


      if [[ ! -f "${_path}" ]]; then
        err "file not found: ${_path}"
        exit 1
      fi

      paths+=( "${_path}" )
    done
  done

  if [[ ${#paths[@]} -eq 0 ]]; then
    err "No files found." && echo
    usage
    exit 1
  fi
}

err() {
    printf "\e[31mError\e[0m %s\n" "${*}"
}

cmd() {
    printf "\e[2m> %s\e[0m\n" "${*}"
}

main() {
  local current srcdir docdir is_dryrun_mode
  local -a paths files versions

  parse ${1:+"$@"}

  srcdir="pythonclass/adventure"
  docdir="docs/exercises/adventure"

  current="$(readlink -f "adventure.py")"
  current="${current##*/}"

  case "${mode}" in
    last)     get_last      ;;
    fin)      get_final     ;;
    version)  get_versions  ;;
    symlinkd) get_symlinkd  ;;
    help)     help ; exit   ;;
  esac

  get_paths

  if [[ -n "${is_dryrun_mode}" ]]; then
    cmd nvim "${paths[@]}"
  else
    nvim "${paths[@]}"
  fi
}

main ${1:+"$@"}
