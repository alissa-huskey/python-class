#!/usr/bin/env zsh
#
# create a symbolic link to a particular version of the adventure
# game and test file if it exists
#
# usage: lnk VERSION
#

setopt nullglob
setopt extendedglob

rootdir="$(cd "${0%/*}/.." && pwd -P)"

usage() {
  cat <<EOF
lnk -- create a symbolic link to a particular version of the adventure
       game and test file if it exists

usage: lnk VERSION
       lnk [-l|-h]

EOF
}

main() {
  local version="$1" srcdir="${rootdir}/pythonclass/adventure" ec=0 mode name files file

  [[ $# -ne 1 ]] && mode=usage ec=1
  [[ "$*" =~ (-h|--help) ]] && mode=usage
  [[ "$*" =~ (-l|--list) ]] && mode=list

  case "${mode}" in
    usage)
      usage
      exit $ec
      ;;
    list)
      file=$(readlink adventure.py)
      version="${file##*-}"
      echo "${version%%.py}"
      exit
      ;;
  esac

  files=( ${srcdir}/*-${version}.py )

  if [[ ${#files[@]} -eq 0 ]]; then
    echo "Error: No files found for version '${version}'"
    exit 1
  fi

  for name in adventure test_game; do
    file="${srcdir}/${name}-${version}.py"
    if ! [[ -f "$file" ]] ; then
      echo "Error: File not found: '${file}'."
      ec=1
      continue
    fi

    ln -vsf "${file}" "${rootdir}/${name}.py"
  done

  exit $ec
}

main ${1:+"$@"}
